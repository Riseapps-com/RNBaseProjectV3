buildscript {
    ext {
        buildToolsVersion = "28.0.3"
        minSdkVersion = 23
        compileSdkVersion = 28
        targetSdkVersion = 28

        gradleVersion = '3.5.3'
        multidexVersion = '2.0.1'
        RNNKotlinVersion = "1.3.61"
        RNNKotlinStdlib = "kotlin-stdlib-jdk8"
        swipeToRefreshLayoutVersion = "1.0.0"
    }
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:$gradleVersion")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${RNNKotlinVersion}")
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        google()
        jcenter()
        maven { url 'https://www.jitpack.io' }
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
    }
}

subprojects { subproject ->
    afterEvaluate {
        if ((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
            android {
                variantFilter { variant ->
                    def names = variant.flavors*.name
                    if (names.contains("reactNative51")
                            || names.contains("reactNative55")
                            || names.contains("reactNative56")
                            || names.contains("reactNative57")
                            || names.contains("reactNative57_5")
                            || names.contains("reactNative57WixFork")
                            || names.contains("reactNative60")) {
                        setIgnore(true)
                    }
                }
            }
        }
    }
}
